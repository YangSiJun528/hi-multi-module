plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
}

allprojects { // 모든(본인포함) 프로젝트에 적용
    group = 'com.multi-module'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
    }
}

subprojects { // 자식 프로젝트에 적용
    apply plugin: 'java' // 위에서 불러온 plugin 적용
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

// 서브 프로젝트 개별 설정
// 이곳에서 lombok 같은 dependency 를 추가 할 수 있으나
// 해당 모듈 내에서도 추가 할 수 있기때문에 프로젝트간의 관계만 명시하도록 하자.
// -> 라고 하는데, 굳이? 그냥 관계 설정도 각 모듈에서 하면 안되나. 이러면 관리하기 오히려 불편할거 같은데
project(':hi-persistence') {
    dependencies {}
}

project(':hi-web') {
    dependencies {
        implementation project(':hi-persistence')
    }
}

project(':hi-batch') {
    dependencies {
        implementation project(':hi-persistence')
    }
}
